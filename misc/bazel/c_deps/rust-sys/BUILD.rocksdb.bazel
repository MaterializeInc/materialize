# Copyright Materialize, Inc. and contributors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License in the LICENSE file at the
# root of this repository, or online at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_skylib//rules:select_file.bzl", "select_file")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")

"""Additive BUILD file for the librocksdb-sys Rust crate."""

# Copied from https://github.com/tensorflow/tensorflow/blob/bdd8bf316e4ab7d699127d192d30eb614a158462/third_party/snappy.BUILD

cc_library(
    name = "snappy",
    srcs = [
        "snappy/snappy.cc",
        "snappy/snappy.h",
        "snappy/snappy-internal.h",
        "snappy/snappy-sinksource.cc",
        "snappy/snappy-sinksource.h",
        "snappy/snappy-stubs-internal.cc",
        "snappy/snappy-stubs-internal.h",
        ":config_h",
        ":snappy_stubs_public_h",
    ],
    hdrs = [
        "snappy/snappy.h",
    ],
    copts = [
        "-DHAVE_CONFIG_H",
        "-fno-exceptions",
        "-Wno-sign-compare",
        "-Wno-shift-negative-value",
        "-Wno-implicit-function-declaration",
    ],
    defines = ["HAVE_SYS_UIO_H"],
    includes = ["snappy/."],
)

genrule(
    name = "config_h",
    outs = ["snappy/config.h"],
    cmd = "\n".join([
        "cat <<'EOF' >$@",
        "#define HAVE_STDDEF_H 1",
        "#define HAVE_STDINT_H 1",
        "",
        "#ifdef __has_builtin",
        "#  if !defined(HAVE_BUILTIN_EXPECT) && __has_builtin(__builtin_expect)",
        "#    define HAVE_BUILTIN_EXPECT 1",
        "#  endif",
        "#  if !defined(HAVE_BUILTIN_CTZ) && __has_builtin(__builtin_ctzll)",
        "#    define HAVE_BUILTIN_CTZ 1",
        "#  endif",
        "#elif defined(__GNUC__) && (__GNUC__ > 3 || __GNUC__ == 3 && __GNUC_MINOR__ >= 4)",
        "#  ifndef HAVE_BUILTIN_EXPECT",
        "#    define HAVE_BUILTIN_EXPECT 1",
        "#  endif",
        "#  ifndef HAVE_BUILTIN_CTZ",
        "#    define HAVE_BUILTIN_CTZ 1",
        "#  endif",
        "#endif",
        "",
        "#ifdef __has_include",
        "#  if !defined(HAVE_BYTESWAP_H) && __has_include(<byteswap.h>)",
        "#    define HAVE_BYTESWAP_H 1",
        "#  endif",
        "#  if !defined(HAVE_UNISTD_H) && __has_include(<unistd.h>)",
        "#    define HAVE_UNISTD_H 1",
        "#  endif",
        "#  if !defined(HAVE_SYS_ENDIAN_H) && __has_include(<sys/endian.h>)",
        "#    define HAVE_SYS_ENDIAN_H 1",
        "#  endif",
        "#  if !defined(HAVE_SYS_MMAN_H) && __has_include(<sys/mman.h>)",
        "#    define HAVE_SYS_MMAN_H 1",
        "#  endif",
        "#  if !defined(HAVE_SYS_UIO_H) && __has_include(<sys/uio.h>)",
        "#    define HAVE_SYS_UIO_H 1",
        "#  endif",
        "#endif",
        "",
        "#ifndef SNAPPY_IS_BIG_ENDIAN",
        "#  ifdef __s390x__",
        "#    define SNAPPY_IS_BIG_ENDIAN 1",
        "#  elif defined(__BYTE_ORDER__) && defined(__ORDER_BIG_ENDIAN__) && __BYTE_ORDER__ == __ORDER_BIG_ENDIAN__",
        "#    define SNAPPY_IS_BIG_ENDIAN 1",
        "#  endif",
        "#endif",
        "EOF",
    ]),
)

genrule(
    name = "snappy_stubs_public_h",
    srcs = ["snappy/snappy-stubs-public.h.in"],
    outs = ["snappy/snappy-stubs-public.h"],
    cmd = ("sed " +
           "-e 's/$${\\(.*\\)_01}/\\1/g' " +
           "-e 's/$${SNAPPY_MAJOR}/1/g' " +
           "-e 's/$${SNAPPY_MINOR}/1/g' " +
           "-e 's/$${SNAPPY_PATCHLEVEL}/4/g' " +
           "$< >$@"),
)

filegroup(
    name = "rocksdb_srcs",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)

cmake(
    name = "rocksdb",
    build_args = ["-j8"],
    generate_args = [
        "-DWITH_SNAPPY=1",
        "-DWITH_LZ4=1",
        "-DWITH_ZSTD=1",
        "-DWITH_GFLAGS=OFF",
        "-DWITH_ALL_TESTS=OFF",
        "-DWITH_TESTS=OFF",
        "-DWITH_TOOLS=OFF",
        "-DUSE_RTTI=1",
        "-DROCKSDB_BUILD_SHARED=OFF",
        # `cmake` tries _very_ hard to find libraries to link against, and it
        # generally prefers dynamic libraries in the sysroot, which is exactly
        # what we don't want because it breaks hermeticity.
        #
        # We set a number of options here to limit what `cmake` will search for
        # so we link against our static libraries.
        "-DCMAKE_FIND_USE_CMAKE_ENVIRONMENT_PATH=0",
        "-DCMAKE_FIND_USE_SYSTEM_ENVIRONMENT_PATH=0",
        "-DCMAKE_FIND_USE_CMAKE_SYSTEM_PATH=0",
        # Uncomment this if you ever need to debug what library cmake is resolving.
        # "-DCMAKE_FIND_DEBUG_MODE=TRUE",
    ] + select(
        {
            "@platforms//os:macos": [
                "-DCMAKE_OSX_DEPLOYMENT_TARGET=14.0",
                "-DCMAKE_SYSTEM_NAME=Darwin",
            ],
            "@platforms//os:linux": ["-DCMAKE_SYSTEM_NAME=Linux"],
        },
        no_match_error = "Building rocksdb for the specified OS is not supported.",
    ) + select(
        {
            "@platforms//cpu:x86_64": ["-DCMAKE_SYSTEM_PROCESSOR=x86_64"],
            "@platforms//cpu:aarch64": ["-DCMAKE_SYSTEM_PROCESSOR=aarch64"],
        },
        no_match_error = "Building rocksdb for the specified CPU is not supported.",
    ),
    lib_source = ":rocksdb_srcs",
    out_static_libs = ["librocksdb.a"],
    targets = ["rocksdb"],
    visibility = ["//visibility:public"],
    working_directory = "rocksdb/",
    deps = [
        ":snappy",
        "@lz4",
        "@zstd",
    ],
)

filegroup(
    name = "out_dir",
    srcs = [":rocksdb"],
    visibility = ["//visibility:public"],
)

select_file(
    name = "librocksdb",
    srcs = ":out_dir",
    subpath = "librocksdb.a",
)

copy_file(
    name = "librocksdb_copy",
    src = ":librocksdb",
    out = "librocksdb.a",
    allow_symlink = False,
)

copy_to_directory(
    name = "rocksdb_lib",
    srcs = [":librocksdb_copy"],
    visibility = ["//visibility:public"],
)

# Copy the include folder so we can specify `ROCKSDB_INCLUDE_DIR`
#
# Note: We used to use `select_file` here but it generated symlinks that
# overlapped with others and spammed the logs with WARNINGs.
copy_to_directory(
    name = "rocksdb_include",
    srcs = [":out_dir"],
    out = "include",
    root_paths = ["rocksdb/include"],
    visibility = ["//visibility:public"],
)

select_file(
    name = "libsnappy",
    srcs = ":snappy",
    subpath = "libsnappy.a",
)

copy_to_directory(
    name = "snappy_lib",
    srcs = [":libsnappy"],
    visibility = ["//visibility:public"],
)

# Licensed to the Apache Software Foundation (ASF) under one or more
# contributor license agreements.  See the NOTICE file distributed with
# this work for additional information regarding copyright ownership.
# The ASF licenses this file to You under the Apache License, Version 2.0
# (the "License"); you may not use this file except in compliance with
# the License.  You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2015 The TensorFlow Authors. All Rights Reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
