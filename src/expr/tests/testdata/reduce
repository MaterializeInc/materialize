# Copyright Materialize, Inc. and contributors. All rights reserved.
#
# Use of this software is governed by the Business Source License
# included in the LICENSE file.
#
# As of the Change Date specified in that file, in accordance with
# the Business Source License, use of this software will be governed
# by the Apache License, Version 2.0.

# IsNull around a single UnaryFunc.

reduce
(
    call_unary is_null
        (call_unary is_null #0)
)
[int32]
----
false

reduce
(
    call_unary is_null
        (call_unary (cast_int_32_to_numeric 127) #0)
)
[int32]
----
(#0) IS NULL

reduce
(
    call_unary is_null
        (call_unary cast_jsonb_to_int32 #0)
)
[jsonb]
----
(#0) IS NULL

# IsNull around a single BinaryFunc.

reduce
(call_unary is_null (call_binary add_int_32 #1 #0)) [(int32 false) (int32 false)]
----
((#0) IS NULL || (#1) IS NULL)

reduce
(call_unary is_null (call_binary and #1 #0)) [(bool false) (bool false)]
----
((#0 && #1)) IS NULL

reduce
(call_unary is_null
    (call_binary (jsonb_get_int64 false) #1 #0))
[(jsonb false) (int64 false)]
----
((#1 -> #0)) IS NULL

# IsNull around multiple functions

reduce
(
    call_unary is_null
        (call_unary is_null
            (call_unary cast_jsonb_to_int32 #0))
)
[jsonb]
----
false

reduce
(
    call_unary is_null
        (call_unary not (call_unary is_null #0))
)
[int32]
----
false

reduce
(
    call_unary is_null
        (call_unary cast_numeric_to_int64
        (call_unary (cast_int_32_to_numeric 127) #0))
)
[int32]
----
(#0) IS NULL

reduce
(
    call_unary is_null
        (call_unary cast_int_64_to_float_64
        (call_unary cast_numeric_to_int64
        (call_unary (cast_int_32_to_numeric 127) #0)))
)
[int32]
----
(#0) IS NULL

reduce
(
    call_unary is_null
        (call_binary add_int_32 #1
            (call_binary mul_int_32 #2 #0))
)
[int32 int32 int32]
----
((#1) IS NULL || ((#0) IS NULL || (#2) IS NULL))

# Null-propagating UnaryFunc around BinaryFunc
reduce
(
    call_unary is_null
    (call_unary (cast_int_32_to_numeric 127)
        (call_binary add_int_32 #1 #0))
)
[(int32 false) (int32 false)]
----
((#0) IS NULL || (#1) IS NULL)

# Non-null-propagating UnaryFunc around BinaryFunc
reduce
(
    call_unary is_null
        (call_unary is_null (call_binary add_int_32 #1 #0))
)
[(int32 false) (int32 false)]
----
false

# Null-propagating BinaryFunc around UnaryFuncs
reduce
(
    call_unary is_null
    (call_binary lt (call_unary is_null #1) (call_unary cast_string_to_bool #0))
)
[string bool]
----
(#0) IS NULL

# Non-null-propagating BinaryFunc around UnaryFuncs
reduce
(
    call_unary is_null
        (call_binary or
            (call_unary is_null #1)
            (call_unary cast_string_to_bool #0))
)
[string bool]
----
(((#1) IS NULL || text_to_boolean(#0))) IS NULL

# outer is_null needs to be resolved in a second round.

reduce
(
    call_unary is_null
        (call_binary or
            (call_unary is_null (call_unary is_null #1))
            (call_unary cast_string_to_bool #0))
)
[string bool]
----
(#0) IS NULL

# Constant folding

reduce
(
    call_unary is_null
        (call_binary add_int_32 #1
            (call_binary mul_int_32 null #0))
)
[int32 int32]
----
true

reduce
(
    call_unary is_null
        (call_binary and
            (call_unary is_null (call_unary is_null #1))
            (call_unary cast_string_to_bool #0))
)
[string bool]
----
false

# Not/demorgans propagation

reduce
(call_unary not
    (call_unary is_null (call_binary add_int_32 #1 #0))
)
[(int32 false) (int32 false)]
----
((#0) IS NOT NULL && (#1) IS NOT NULL)

canonicalize
[(call_unary not
    (call_unary is_null (call_binary add_int_32 #1 #0))
)]
[(int32 false) (int32 false)]
----
(#0) IS NOT NULL
(#1) IS NOT NULL

reduce
(
    call_unary not
        (
            call_binary and
                (call_binary gt #1 #0)
                (call_binary lt #1 #2)
        )
)
[(int32 false) (int32 false) (int32 false)]
----
((#1 <= #0) || (#1 >= #2))

reduce
(
    call_unary not
        (
            call_binary and
                (call_unary not #0)
                (call_binary lt #1 (call_unary not #2))
        )
)
[(bool) (int32 false) (int32 false)]
----
(#0 || (#1 >= NOT(#2)))

reduce
(
    call_unary not
        (
            call_binary and
                (call_unary not #0)
                (call_binary or #1 (call_unary not #2))
        )
)
[bool bool bool]
----
(#0 || (#2 && NOT(#1)))

# undistribute and/or

reduce
(call_binary or
    (call_binary and
        (call_unary not #1) #0)
        (call_binary and (call_unary not #1) #2))
[bool bool bool]
----
(NOT(#1) && (#0 || #2))

reduce
(call_unary not
    (call_binary or
        (call_binary and #1 #0)
        (call_binary and #1 #2)))
[bool bool bool]
----
(NOT(#1) || (NOT(#0) && NOT(#2)))

reduce
(call_unary not (call_binary and (call_binary or #1 #0)
        (call_binary or #1 #2)))
[bool bool bool]
----
(NOT(#1) && (NOT(#0) || NOT(#2)))

reduce
(call_binary and
    (call_unary is_null (call_binary add_int32 #1 (call_unary cast_int64_to_int32 #0)))
    (call_unary is_null (call_binary add_int32 #2 #0))
)
[int64 int32 int32]
----
((#0) IS NULL || ((#1) IS NULL && (#2) IS NULL))

reduce
(call_binary and (call_binary or (call_binary or #0 #1) (call_binary gt #2 2))
    (call_binary or #0 (call_binary or (call_binary lt #2 3) #1)))
[bool bool int32]
----
(#1 || (#0 || ((#2 < 3) && (#2 > 2))))

## a | (a & b)

reduce
(call_binary or #0
    (call_binary and #0 (call_binary lt #2 3)))
[bool bool int32]
----
#0

## a & (a | b)

reduce
(call_binary and (call_binary or #0 #1)
    (call_binary or #0 (call_binary or (call_binary lt #2 3) #1)))
[bool bool int32]
----
(#0 || #1)

## Record get/create optimizations

reduce
(call_unary (record_get 0) (call_variadic (record_create ["f1" "f2"]) [#0 #1]))
[int32 int32]
----
#0

reduce
(call_unary (record_get 0)
    (call_variadic coalesce
        [
            (call_variadic (record_create ["f1" "f2"]) [#0 #1])
            (call_variadic (record_create ["f1" "f2"]) [(null int32) (null int32)])
        ]
    )
)
[int32 int32]
----
#0

## list_index(list_create, literal), e.g., list[f1, f2][2] --> f2
## See rest of the tests for this in list.slt

reduce
(call_variadic list_index [
    (call_variadic (list_create int32) [#1 #2])
    2
])
[int32 int32 int32]
----
#2

## Case/If optimizations

reduce
(if (call_binary gt #0 #1) false true)
[int32 int32]
----
(((#0) IS NULL || (#1) IS NULL) || (#0 <= #1))

reduce
(if (call_binary gt #0 #1) false (null bool))
[int32 int32]
----
(null && (((#0) IS NULL || (#1) IS NULL) || (#0 <= #1)))

reduce
(if (call_binary gt #0 #1) false false)
[int32 int32]
----
false

# non-literal expression in the THEN clause
reduce
(if (call_binary gt #0 #1) (call_binary eq #0 (1 int32)) false)
[int32 int32]
----
if (#0 > #1) then {(#0 = 1)} else {false}

reduce
(if (call_binary gt #0 #1) (null bool) false)
[int32 int32]
----
(null && (((#0) IS NOT NULL && (#1) IS NOT NULL) && (#0 > #1)))

# non-literal expression in the THEN clause
reduce
(if (call_binary gt #0 #1) true false)
[int32 int32]
----
(((#0) IS NOT NULL && (#1) IS NOT NULL) && (#0 > #1))

reduce
(if (call_binary gt #0 #1) true (null bool))
[int32 int32]
----
(null || (((#0) IS NOT NULL && (#1) IS NOT NULL) && (#0 > #1)))

reduce
(if (call_binary gt #0 #1) (call_binary eq #0 (1 int32)) true)
[int32 int32]
----
if (#0 > #1) then {(#0 = 1)} else {true}

reduce
(if (call_binary gt #0 #1) (null bool) true)
[int32 int32]
----
(null || (((#0) IS NULL || (#1) IS NULL) || (#0 <= #1)))

reduce
(if (call_binary gt #0 #1) true true)
[int32 int32]
----
true

reduce
(if (call_binary gt #0 #1) (null bool) (null bool))
[int32 int32]
----
null

reduce
(if (call_binary gt #0 #1) (null int32) (null int32))
[int32 int32]
----
null

reduce
(if (call_binary gt #0 #1) (1 int32) (2 int32))
[int32 int32]
----
if (#0 > #1) then {1} else {2}

reduce
(if (null bool) true false)
[]
----
false

reduce
(if (null bool) false true)
[]
----
true

reduce
(if (null bool) #0 false)
[bool]
----
false

reduce
(if (null bool) #0 true)
[bool]
----
true

reduce
(if (null bool) false #0)
[bool]
----
#0

reduce
(if (null bool) true #0)
[bool]
----
#0

reduce
(if true true false)
[]
----
true

reduce
(if true false true)
[]
----
false

reduce
(if true #0 false)
[bool]
----
#0

reduce
(if true #0 true)
[bool]
----
#0

reduce
(if true false #0)
[bool]
----
false

reduce
(if true true #0)
[bool]
----
true

reduce
(if false true false)
[]
----
false

reduce
(if false false true)
[]
----
true

reduce
(if false #0 false)
[bool]
----
false

reduce
(if false #0 true)
[bool]
----
true

reduce
(if false false #0)
[bool]
----
#0

reduce
(if false true #0)
[bool]
----
#0

### Regression test for #9995.
### The inner if statement can be replaced by its `condition`, but we must
### ensure that we keep the type of the `then` and `els` clauses.
### The type of the error should be int32 instead of bool.

reduce
(if
    (call_binary eq #0 1)
    1
    (if
        (call_binary eq #0 (call_binary div_int64 1 0))
        1
        1
    )
)
[int64]
----
if (#0 = 1) then {1} else {(err: division by zero)}

## undistribute_and/or works despite multiple copies of the same expression in
## the intersection

canonicalize
[(call_binary or
    (call_binary and
        (call_unary not (call_unary is_null #1))
        (call_binary lt #0 #2))
    (call_unary not (call_binary or (call_unary is_null #1) (call_binary or
    (call_unary is_null #1) (call_unary is_null #3)))))]
[float64 float64 float64 float64]
----
(#1) IS NOT NULL
((#3) IS NOT NULL || (#0 < #2))

canonicalize
[(call_binary or
    (call_unary not (call_unary is_null (call_binary add_float_64 (call_binary add_float_64 #3 #1) #1)))
        (call_binary and
        (call_unary not (call_unary is_null #1))
        (call_binary lt #0 #2)))]
[float64 float64 float64 float64]
----
(#1) IS NOT NULL
((#3) IS NOT NULL || (#0 < #2))

# expressions in equivalence classes only become simpler.

canonicalize-join
[[
    (call_binary and #0 (call_binary add_int32 #0 #0))
    (call_binary add_int32 #0 #0)
    (call_binary add_int32 #0 (call_binary add_int32 #0 #0))
]]
[int32]
----
[(#0 && (#0 + #0)) (#0 + #0) (#0 + (#0 + #0))]

canonicalize-join
[
    [#0 #3]
    [#1
        (call_binary add_int32 (call_binary add_int32 #2 #2) #1)
        (call_binary add_int32
            (call_binary add_int32 #2 #2)
            (call_binary add_int32 (call_binary add_int32 #0 #0) #0))
        (call_binary add_int32 (call_binary add_int32 #3 #3) #3)
    ]
]
[int32 int32 int32 int32]
----
[#0 #3]
[#1 ((#0 + #0) + #0) ((#2 + #2) + #1)]

canonicalize-join
[
    [#0 #3]
    [#1
        (call_binary add_int32 (call_binary add_int32 #2 #2) #1)
        (call_binary add_int32
            (call_binary mul_int32 #4 #5)
            (call_binary add_int32 (call_binary add_int32 #0 #0) #0))
        (call_binary add_int32 (call_binary add_int32 #3 #3) #3)
    ]
    [(call_binary add_int32 #2 #2) (call_binary mul_int32 #4 #5)]
]
[int32 int32 int32 int32 int32 int32]
----
[#0 #3]
[#1 ((#0 + #0) + #0) ((#2 + #2) + #1)]
[(#2 + #2) (#4 * #5)]

# replacing expressions with simpler equivalent ones can result in the
# collapsing of equivalence classes.

canonicalize-join
[
    [#0 #3]
    [#1
        (call_binary add_int32 #0 #0)
        (call_binary add_int32 (call_binary add_int32 #2 #2) #1)
        (call_binary add_int32
            (call_binary mul_int32 #4 #5)
            (call_binary add_int32 (call_binary add_int32 #0 #0) #0))
        (call_binary add_int32 (call_binary add_int32 #3 #3) #3)
    ]
    [(call_binary add_int32 #3 #3) (call_binary mul_int32 #4 #5) (call_binary
    sub_int32 (call_binary add_int32 #3 #3) (call_binary mul_int32 #4 #5))]
]
[int32 int32 int32 int32 int32 int32]
----
[#0 #3]
[#1 (#0 + #0) (#1 + #0) (#1 + #1) ((#2 + #2) + #1) (#1 - #1) (#4 * #5)]

canonicalize-join
[[#0 #3 #3] [(call_binary add_int32 #0 #0) #1] [(call_binary add_int32 #3 #3)
#2]]
[int32 int32 int32 int32]
----
[#0 #3]
[#1 #2 (#0 + #0)]

# replacing expressions with simpler equivalent ones can result in the
# removal of redundant equivalence classes.

canonicalize-join
[[#0 #0 #3] [(call_binary add_int32 #0 #0) (call_binary add_int32 #3 #3)]]
[int32 int32 int32 int32]
----
[#0 #3]

# test an equivalence class when the number of leaves are the same but the
# number of nonleaves are not.

canonicalize-join
[[
    (call_unary cast_int16_to_int32 #0)
    (call_unary neg_int32 (call_unary cast_int16_to_int32 #0))
    (call_unary neg_int32 (call_unary neg_int32 (call_unary cast_int16_to_int32
    #0)))
]]
[int32]
----
[-(smallint_to_integer(#0)) smallint_to_integer(#0)]

# literals don't get overwritten with equivalent expressions

canonicalize-join
[[
    #0
    (4 int32)
    (call_binary add_int32 #1 (4 int32))
]]
[int32 int32]
----
[#0 4 (#1 + 4)]

# functions on literals don't cause cycling
canonicalize-join
[
    [#0 (4 int32)]
    [(call_unary neg_int32 #0) (call_unary neg_int32 #1) (call_unary neg_int32 (4 int32))]
    [(call_binary add_int32 #1 (call_unary neg_int32 #1)) #3]
]
[int32 int32 int32 int32]
----
[#0 4]
[#3 (#1 + -4)]
[-4 -(#1)]

canonicalize-join
[
    [#0 (4 int32)]
    [(call_unary neg_int32 #0) (call_unary neg_int32 #1)]
    [(call_unary neg_int32 #1) (call_unary neg_int32 (4 int32))]
    [(call_binary add_int32 #1 (call_unary neg_int32 #1)) #3]
]
[int32 int32 int32 int32]
----
[#0 4]
[#3 (#1 + -4)]
[-4 -(#1)]

# expressions in join equivalences get reduced after simpler equivalent
# expressions are substituted

## constant folding

canonicalize-join
[
    [0 (call_binary add_int64 0 #0)]
    [1234 (call_binary add_int64 (call_binary add_int64 0 #0) 0)]
]
[int64]
----
[0 1234 (0 + #0)]

## consecutive nots cancel each other

canonicalize-join
[
    [(call_unary not #0) (call_binary (is_regexp_match true) #1 #2)]
    [false (call_unary not (call_binary (is_regexp_match true) #1 #2))]
]
[bool string string]
----
[#0 false]
[true (#1 ~* #2)]

canonicalize-join
[
    [(call_unary not #0) (call_binary or #0 #1)]
    [false (call_unary not (call_binary or #0 #1))]
]
[bool bool]
----
[#0 false]
[#1 true]

## demorgans

canonicalize-join
[
    [ (call_binary and #2 #3) (call_variadic coalesce [#0 #1 false])]
    [ #0 (call_binary or (call_variadic coalesce [#0 #1 false]) (call_binary and #2 #4))]
]
[bool bool bool bool bool]
----
[#0 (#2 && (#3 || #4))]
[(#2 && #3) coalesce(#0, #1, false)]

## decompose is_null

canonicalize-join
[
    [ (call_binary add_int32 #2 #3) (call_variadic coalesce [#0 #1 false])]
    [ #0 (call_unary is_null (call_variadic coalesce [#0 #1 false]))]
]
[int32 int32 int32 int32]
----
[#0 ((#2) IS NULL || (#3) IS NULL)]
[(#2 + #3) coalesce(#0, #1, false)]

# impossible condition detection during predicate canonicalization produces
# reliable output regardless of the order of the input predicates
canonicalize
[(call_binary eq #0 10) (call_unary is_null #0)]
[int64]
----
false

canonicalize
[
    (call_binary or
        (call_binary and
            (call_unary is_null #0)
            (call_unary is_null #1))
        (call_binary eq #0 #1))
    (call_binary or
        (call_binary and
            (call_unary is_null #0)
            (call_unary is_null #2))
        (call_binary eq #0 #2))
    (call_unary not (call_unary is_null #0))
    (call_unary not (call_unary is_null #1))
    (call_unary not (call_unary is_null #0))
    (call_unary not (call_unary is_null #2))
]
[int64 int64 int64]
----
(#0 = #1)
(#0 = #2)

canonicalize
[
    (call_binary or
        (call_binary and
            (call_unary is_null #0)
            (call_unary is_null #1))
        (call_binary eq #0 #1))
    (call_binary or
        (call_binary and
            (call_unary is_null #0)
            (call_unary is_null #2))
        (call_binary eq #0 #2))
    (call_unary not (call_unary is_null #0))
    (call_unary not (call_unary is_null #1))
    (call_unary not (call_unary is_null #1))
    (call_unary not (call_unary is_null #2))
]
[int64 int64 int64]
----
(#0 = #1)
(#0 = #2)

canonicalize
[
    (call_binary or
        (call_binary and
            (call_unary is_null #1)
            (call_unary is_null #2))
        (call_binary eq #1 #2))
    (call_binary eq #0 #1)
    (call_binary eq #0 #2)
]
[int64 int64 int64]
----
(#0 = #1)
(#0 = #2)
(#1 = #2)

# check that predicates are sorted by their complexity
canonicalize
[
    (call_binary eq (call_binary add_int32 #0 (1 int32)) #1)
    (call_binary lt #0 (2147483647 int32))
]
[int32 int32]
----
(#0 < 2147483647)
(#1 = (#0 + 1))

canonicalize
[
    (call_binary lt (call_binary add_int32 #0 (1 int32)) #2)
    (call_binary lt (call_binary add_int32 #0 (1 int32)) (2147483647 int32))
    (call_binary eq #0 #1)
]
[int32 int32 int32]
----
(#0 = #1)
((#0 + 1) < 2147483647)
((#0 + 1) < #2)
